/*
  SQL Script to determine the size of the tables for a SQL Server Database

  [SOME_DATABASE]   
*/

USE [SOME_DATABASE];

SET NOCOUNT ON
IF OBJECT_ID('TEMPDB..#SPACEUSED') IS NOT NULL DROP TABLE #SPACEUSED

CREATE TABLE #SPACEUSED
(
  TABLENAME SYSNAME ,
  [ROWS] INT ,
  [RESERVED] VARCHAR(20),
  [DATA] VARCHAR(20),
  [INDEX_SIZE] VARCHAR(20),
  [UNUSED] VARCHAR(20),
  [RESERVED_KB] BIGINT,
  [DATA_KB] BIGINT,
  [INDEX_SIZE_KB] BIGINT,
  [UNUSED_KB] BIGINT
)

DECLARE @CMD NVARCHAR(MAX) =''
SELECT @CMD +='EXEC SP_SPACEUSED ' +  ''''+QUOTENAME(TABLE_SCHEMA)+'.'+ QUOTENAME(TABLE_NAME)+''''+';'+CHAR(10)
FROM INFORMATION_SCHEMA.TABLES
--PRINT @CMD

INSERT INTO #SPACEUSED
  (TABLENAME ,[ROWS] , [RESERVED], [DATA] , [INDEX_SIZE] , [UNUSED] )
EXEC SP_EXECUTESQL @CMD



UPDATE #SPACEUSED 
     SET [RESERVED_KB] = CONVERT(BIGINT,RTRIM(LTRIM(REPLACE([RESERVED] , ' KB', '')))),
         [DATA_KB] = CONVERT(BIGINT,RTRIM(LTRIM(REPLACE([DATA] , ' KB', '')))),
         [INDEX_SIZE_KB]= CONVERT(BIGINT,RTRIM(LTRIM(REPLACE([INDEX_SIZE] , ' KB', '')))),
         [UNUSED_KB]= CONVERT(BIGINT,RTRIM(LTRIM(REPLACE([UNUSED] , ' KB', ''))))


SELECT TABLENAME,
  [ROWS],
  DATA_KB / 1024.0 / 1024.0 DATA_GB,
  DATA_KB / 1024.0 DATA_MB,
  RESERVED_KB / 1024.0 RESERVED,
  INDEX_SIZE_KB / 1024.0 INDEX_SIZE,
  UNUSED_KB / 1024.0 UNUSED
FROM #SPACEUSED
ORDER BY DATA_KB DESC

SELECT SUM(RESERVED_KB) RESERVED_KB , SUM(DATA_KB) DATA_KB, SUM(INDEX_SIZE_KB) INDEX_SIZE_KB , SUM(UNUSED_KB) UNUSED_KB , SUM(DATA_KB / 1024.0) DATA_MB , SUM(DATA_KB / 1024.0 / 1024.0) DATA_GB
FROM #SPACEUSED

DROP TABLE #SPACEUSED